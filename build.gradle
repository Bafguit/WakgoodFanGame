import java.security.MessageDigest

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

plugins{
    id "io.freefair.lombok" version "6.5.1" apply false
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id "distribution"
    //id "com.diffplug.spotless" version "6.11.0"
}
allprojects {
    apply plugin: "eclipse"
    version = 'b0.9.25'
    ext {
        appName = "Labyrintale"
        gdxVersion = '1.10.0'
        roboVMVersion = '2.3.12'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.3'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.1.0'
        spineVersion = '3.8.55.1'
        videoVersion = '1.3.2-SNAPSHOT'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
        flatDir {
            dir '../libs'
        }
    }
}

project(":android") {
    apply plugin: "com.android.application"

    configurations { natives }

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"
        api "de.golfgl.gdxgamesvcs:gdx-gamesvcs-android-gpgs:1.1.0"
        implementation "com.badlogicgames.gdx-video:gdx-video-android:$videoVersion"

    }
}

project(":desktop") {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "distribution"
    
    dependencies {
        implementation project(":core")
        implementation 'com.opencsv:opencsv:5.6'
        implementation 'org.reflections:reflections:0.10.2'
        api "com.badlogicgames.gdx-video:gdx-video-lwjgl3:$videoVersion"
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        api "de.golfgl.gdxgamesvcs:gdx-gamesvcs-core-gamejolt:1.0.2"
        api("com.badlogicgames.gdx:gdx-tools:$gdxVersion"){
            exclude group: 'com.badlogicgames.gdx', module: 'gdx-backend-lwjgl'
        }
    }
    distributions {
        main{
            distributionBaseName = "WakestDungeon"
            contents{
                from "../bin"
                from jar.destinationDir
            }
        }
    }

}


project(":core") {
    apply plugin: "java"
    apply plugin: "java-library"

    dependencies {

        implementation 'com.google.guava:guava:31.1-jre'
        implementation 'org.reflections:reflections:0.10.2'
        implementation "com.badlogicgames.gdx-video:gdx-video:$videoVersion"
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "com.esotericsoftware.spine:spine-libgdx:$spineVersion"
        api "de.golfgl.gdxgamesvcs:gdx-gamesvcs-core:1.0.2"
        api "com.fasterxml.jackson.core:jackson-databind:2.13.3"
        api 'com.opencsv:opencsv:5.6'

    }
}

def props = new Properties()
def propertiesFile = file(".gradle/gradle.properties")
if(propertiesFile.exists())
    propertiesFile.withInputStream { props.load(it) }
githubRelease {
    token props.getProperty("githubToken","token")
    owner "Bafguit"
    repo "WakgoodFanGame"
    tagName version.toString()
    targetCommitish "master"
    releaseName "v${version.toString()}"
    generateReleaseNotes true
    releaseAssets project(":desktop").distsDirectory.asFile.get().listFiles()
    overwrite true
    allowUploadToExisting.set true
}
distZip{
    dependsOn ':desktop:dist'
}
def md5( obj ) {
    def hash = MessageDigest.getInstance( 'MD5' ).with {
        obj.eachByte( 8192 ) { bfr, num ->
            update bfr, 0, num
        }
        it.digest()
    }
    new BigInteger( 1, hash ).toString( 16 ).padLeft( 32, '0' )
}
//https://gist.github.com/ikarius/299062/85b6540c99878f50f082aaee236ef15fc78e527c
task createVersion(){
    doLast {
        def zipFile = project(":desktop").distsDirectory.asFile.get().listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return name.endsWith(".zip")
            }
        })[0]
        if(zipFile == null)
            throw new IllegalStateException("Zip file cannot be null")

        def jarFile = project(":desktop").jar.destinationDir.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return name.endsWith(".zip")
            }
        })[0]

        if(jarFile == null)
            throw new IllegalStateException("jar file cannot be null")

        def output = new File("version.txt")

        output.write("$version ${zipFile.name} ${md5(zipFile)} ${md5(jarFile)}")
    }
}


