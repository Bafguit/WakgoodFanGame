plugins{
    id "io.freefair.lombok" version "6.5.1" apply false
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id "distribution"
    id "com.diffplug.spotless" version "6.11.0"
}
allprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "com.diffplug.spotless"
    version = 'b0.6'
    ext {
        appName = "Labyrintale"
        gdxVersion = '1.10.0'
        roboVMVersion = '2.3.12'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.3'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.1.0'
        spineVersion = '3.8.55.1'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    spotless {
        enforceCheck true
        java{
            importOrder()

            trimTrailingWhitespace()
            indentWithSpaces()
            indentWithTabs()
            endWithNewline()
            removeUnusedImports()
            formatAnnotations()

            googleJavaFormat()

        }
    }
}
project(":desktop") {


    apply plugin: "java-library"
    apply plugin: "distribution"

    dependencies {
        implementation project(":core")
        implementation 'com.opencsv:opencsv:5.6'
        implementation 'org.reflections:reflections:0.10.2'
        api "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        api "de.golfgl.gdxgamesvcs:gdx-gamesvcs-core-gamejolt:1.0.2"
        api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
    distributions {
        main{
            distributionBaseName = "WakestDungeon"
            contents{
                from "../bin"
                from jar.destinationDir
            }
        }
    }

}

project(":core") {
    apply plugin: "java-library"

    dependencies {
        implementation 'org.reflections:reflections:0.10.2'
        implementation "com.badlogicgames.gdx-video:gdx-video:1.3.2-SNAPSHOT"
        implementation "com.badlogicgames.gdx-video:gdx-video-lwjgl:1.3.2-SNAPSHOT"
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "com.esotericsoftware.spine:spine-libgdx:$spineVersion"
        api "de.golfgl.gdxgamesvcs:gdx-gamesvcs-core:1.0.2"
        api "com.fasterxml.jackson.core:jackson-databind:2.13.3"
        api 'com.opencsv:opencsv:5.6'

    }
}

def props = new Properties()
def propertiesFile = file(".gradle/gradle.properties")
if(propertiesFile.exists())
    propertiesFile.withInputStream { props.load(it) }
githubRelease {
    token props.getProperty("githubToken","token")
    owner "Bafguit"
    repo "WakgoodFanGame"
    tagName version.toString()
    targetCommitish "master"
    releaseName "v${version.toString()}"
    generateReleaseNotes true
    releaseAssets project(":desktop").distsDirectory.asFile.get().listFiles()
    overwrite true
    allowUploadToExisting.set true
}
distZip{
    dependsOn ':desktop:dist'
}


